;This routine uses a time-base delay, which should work with the newer (2019)
;calculator models whose LCD has a malfunctioning status bit.
;
;The TI-83+/84+ and similar calculators have a clock speed that varies based on
;a number of factors, so we'll assume it is up to 6.6MHz. According to TI's
;documentation, we'll need 10 microseconds beteen writes to the LCD, so that's
;66 clock cycles at 6.6MHz
;Since an out takes at least 11cc, we'll aim for 55cc between outs

#ifndef included_gbuf_to_lcd_6MHZ
#define included_gbuf_to_lcd_6MHZ

gbuf_to_lcd_6MHz:
;This copies the contents of gbuf to the LCD.
;Preserves all registers.
  push hl     ;11   11
  push de     ;11   22
  push bc     ;11   33
  push af     ;11   44
  ld hl,gbuf  ;10   54
  ld a,$80    ;7    61
  out (16),a  ;11   0

  ld de,11    ;10   10
  ld a,$20    ;7    17
  ex (sp),hl  ;19   36 \ waste clock cycles
  ex (sp),hl  ;19   55 /
col:
  out (10h),a ;11   0
  ld b,(hl)   ;7    7
  xor b       ;4    11  \ waste clock cycles
  xor b       ;4    15  /
  ld bc,$8011 ;10   25
  nop         ;4    29

row:
  sbc hl,de   ;15   44  \ waste clock cycles
  add hl,de   ;11   55  /
  outi        ;16   5
  add hl,de   ;11   16
  djnz row    ;13/8 29/24
  inc a       ;4    28
  dec h       ;4    32
  dec h       ;4    36
  dec h       ;4    40
  inc hl      ;6    46
  cp $2C      ;7    53
  jp nz,col   ;10   63
  pop af      ;10
  pop bc      ;10
  pop de      ;10
  pop hl      ;10
  ret         ;10
#endif

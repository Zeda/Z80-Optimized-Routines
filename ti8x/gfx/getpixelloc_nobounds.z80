getpixelloc:
;Input: (B,C) is (X,Y)
;Output: A is the mask, HL is the byte the pixel is on
;Destroys: BC
;37 bytes (or 40 bytes if the pixLUT crosses a 256-byte boundary)
;155cc (or 166~167  if the pixLUT crosses a 256-byte boundary)

  ld a,b
  ld b,0
  ld h,b
  ld l,c
  add hl,hl
  add hl,bc
  add hl,hl
  add hl,hl
  ld c,a

#if (plotSScreen&255) = 64
  srl c
  scf
  rr c
  srl c
  ld b,plotSScreen>>8
#else
  srl c
  srl c
  srl c
  add hl,bc
  ld bc,plotSScreen
#endif
  add hl,bc

  and 7
  ld bc,pixLUT
  add a,c
  ld c,a
#if (pixLUT&255)>248
  jr nc,$+3
  inc b
#endif
  ld a,(bc)
  ret

pixLUT:
  .db $80,$40,$20,$10,$08,$04,$02,$01
